buildscript {
  repositories {
    maven { url = "http://files.minecraftforge.net/maven" }
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
  }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

version = "0.1.6"
group = "com.lovetropics"
archivesBaseName = "ltperms"

sourceCompatibility = targetCompatibility = '1.8'

minecraft {
  mappings channel: 'snapshot', version: '20210309-1.16.5'
  runs {
    client {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      mods {
        ltperms {
          sources sourceSets.main
        }
      }
    }
    server {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'REGISTRIES'
      property 'forge.logging.console.level', 'debug'
      mods {
        ltperms {
          sources sourceSets.main
        }
      }
    }
  }
}


configurations {
  shade
  shadelib
  implementation.extendsFrom shadelib
}

repositories {
  maven { url "http://maven.tterrag.com/" }
  maven { url "https://maven.enginehub.org/repo/" }

  flatDir { dirs 'libs/' }
}

dependencies {
  minecraft 'net.minecraftforge:forge:1.16.5-36.1.2'

  def ltlib = "com.lovetropics.lib:LTLib:1.1.0-release+3"
  implementation fg.deobf(ltlib)
  shade ltlib

  implementation "com.sk89q.worldedit:worldedit-core:7.2.6-SNAPSHOT"
  implementation fg.deobf("com.sk89q.worldedit:worldedit-forge-mc1.16.3:7.2.6-SNAPSHOT")

  implementation fg.deobf('com.github.hexomod:WorldEdit-CUI-FE3:1.16.5-3.0.9')
}

shadowJar {
  configurations = [project.configurations.shade, project.configurations.shadelib]
  relocate 'com.lovetropics.lib', 'com.lovetropics.perms.repack.ltlib'
  classifier = ''
}

reobf {
  shadowJar {}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

jar {
  classifier = 'slim'
  manifest {
    attributes([
        "Specification-Title"     : "ltperms",
        "Specification-Vendor"    : "lovetropics",
        "Specification-Version"   : "1",
        "Implementation-Title"    : project.name,
        "Implementation-Version"  : "${version}",
        "Implementation-Vendor"   : "lovetropics",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
  }
}
