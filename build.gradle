buildscript {
  repositories {
    maven { url = 'https://maven.minecraftforge.net' }
    maven { url = 'https://maven.parchmentmc.org' }
    mavenCentral()
  }
  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    classpath "org.parchmentmc:librarian:1.+"
  }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'

version = "0.2.0"
group = "com.lovetropics"
archivesBaseName = "ltperms"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
  mappings channel: 'parchment', version: "${parchment_version}-${minecraft_version}"
  runs {
    client {
      workingDirectory project.file('run')
      property 'forge.logging.console.level', 'info'
      mods {
        ltperms {
          sources sourceSets.main
        }
      }
    }
    server {
      workingDirectory project.file('run')
      property 'forge.logging.console.level', 'info'
      mods {
        ltperms {
          sources sourceSets.main
        }
      }
    }
  }
}


configurations {
  shade
}

repositories {
  maven { url "https://maven.tterrag.com/" }
  maven { url "https://maven.enginehub.org/repo/" }

  flatDir { dirs 'libs/' }
}

dependencies {
  minecraft "net.minecraftforge:forge:$minecraft_version-$forge_version"

  def ltlib = "com.lovetropics.lib:LTLib:1.2.0-release+3"
  implementation fg.deobf(ltlib)
  shade ltlib

  implementation "com.sk89q.worldedit:worldedit-core:7.2.11-SNAPSHOT"
  implementation fg.deobf("com.sk89q.worldedit:worldedit-forge-mc1.18.2:7.2.11-SNAPSHOT")

//  implementation fg.deobf('com.github.hexomod:WorldEdit-CUI-FE3:1.16.5-3.0.9')
}

shadowJar {
  configurations = [project.configurations.shade]
  relocate 'com.lovetropics.lib', 'com.lovetropics.perms.repack.ltlib'
  classifier = ''
}

reobf {
  shadowJar {}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

jar {
  classifier = 'slim'
  manifest {
    attributes([
        "Specification-Title"     : "ltperms",
        "Specification-Vendor"    : "lovetropics",
        "Specification-Version"   : "1",
        "Implementation-Title"    : project.name,
        "Implementation-Version"  : "${version}",
        "Implementation-Vendor"   : "lovetropics",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
  }
}
